type BoundaryFn[T]   = fn(i32, i32, i32) -> BoundaryMode[T];
type StencilFn[T]    = fn(Intrinsics, i32, i32, Acc[T], AccList[T]) -> ();
type StencilSepFn[T] = fn(Intrinsics, i32, i32, Acc[T], Acc[T], MaskSep, bool) -> ();
type Region          = (Boundary, Boundary);

struct Img {
    buf : Buffer,
    width  : i32,
    height : i32,
    stride : i32,
}

struct RoI {
    o_x : i32,
    o_y : i32,
    width : i32,
    height : i32
}

struct Acc[T] {
    read  : fn(i32, i32) -> T,
    write : fn(i32, i32, T) -> (),
}

struct Mask {
    data : fn(i32, i32) -> f32,
    size_x : i32,
    size_y : i32,
}

struct MaskSep {
    data : fn(i32) -> f32,
    size : i32,
}

enum Boundary {
    Lower, Center, Upper, Unknown
}

enum BoundaryMode[T] {
    Index(i32),
    Const(T)
}

fn @apply_stencil[T](x: i32, y: i32, acc: Acc[T], mask: Mask) -> T {
    let mut sum = 0:f32;
    let half_range_y = mask.size_y / 2;
    let half_range_x = mask.size_x / 2;

    for j in unroll(-half_range_y, half_range_y+1) {
        for i in unroll(-half_range_x, half_range_x+1) {
            let stencil_val = mask.data(j + half_range_y, i + half_range_x);
            if stencil_val != 0:f32 {
                sum += bitcast[f32](acc.read(x + i, y + j)) * stencil_val;
            } else {
                // additional logic for empty stencil value
            }
        }
    }

    bitcast[T](sum)
}

fn @apply_stencil_sep[T](x: i32, y: i32, acc: Acc[T], mask: MaskSep, row: bool) -> T {
    let mut sum = 0:f32;
    let half_range = mask.size / 2;

    for i in unroll(-half_range, half_range+1) {
        let stencil_val = mask.data(i + half_range);
        if stencil_val != 0:f32 {
            if row {
                sum += bitcast[f32](acc.read(x + i, y)) * stencil_val;
            } else {
                sum += bitcast[f32](acc.read(x, y + i)) * stencil_val;
            }
        } else {
            // additional logic for empty stencil value
        }
    }

    bitcast[T](sum)
}

// boundary handling functions
fn @get_bounds(lower: i32, upper: i32, bh_pixels: i32, boundary: Boundary) -> (i32, i32) {
    match boundary {
        Boundary::Lower  => (lower, bh_pixels),
        Boundary::Center => (bh_pixels, upper - bh_pixels),
        Boundary::Upper  => (upper - bh_pixels, upper),
        _                => undef[(i32,i32)]()
    }
}
fn @get_boundary(boundary: i32) -> Boundary {
    match boundary {
        0 => Boundary::Lower,
        1 => Boundary::Center,
        2 => Boundary::Upper,
        _ => undef[Boundary]()
    }
}
fn @iterate_1dregion(body: fn((i32, i32), Boundary) -> ()) = @|lower: i32, upper: i32, tile_size: i32| {
    if tile_size == 0 {
        let boundary:Boundary = Boundary::Center;
        @body((lower, upper), boundary)
    } else {
        for region in unroll(0, 3) {
            let boundary = get_boundary(region);
            let bounds   = get_bounds(lower, upper, tile_size, boundary);
            @body(bounds, boundary)
        }
    }
}
fn @iterate_2dregion(body: fn(((i32, i32), (i32, i32)), Region) -> ()) = @|(lower_row: i32, lower_col: i32), (upper_row: i32, upper_col: i32), (tile_size_row: i32, tile_size_col: i32)| {
    for bounds_row, boundary_row in iterate_1dregion(lower_row, upper_row, tile_size_row) {
        for bounds_col, boundary_col in iterate_1dregion(lower_col, upper_col, tile_size_col) {
            @body((bounds_row, bounds_col), (boundary_row, boundary_col))
        }
    }
}

fn @nobh_center[T](idx: i32, _lower: i32, _upper: i32) -> BoundaryMode[T] { BoundaryMode[T]::Index(idx) }
fn @clamp_lower[T](idx: i32, lower: i32, _upper: i32) -> BoundaryMode[T] {
    BoundaryMode[T]::Index(if idx < lower { lower } else { idx })
}
fn @clamp_upper[T](idx: i32, _lower: i32, upper: i32) -> BoundaryMode[T] {
    BoundaryMode[T]::Index(if idx >= upper { upper - 1 } else { idx })
}
fn @mirror_lower[T](idx: i32, lower: i32, _upper: i32) -> BoundaryMode[T] {
    BoundaryMode[T]::Index(if idx < lower { lower + (lower - idx-1) } else { idx })
}
fn @mirror_upper[T](idx: i32, _lower: i32, upper: i32) -> BoundaryMode[T] {
    BoundaryMode[T]::Index(if idx >= upper { upper - (idx+1 - upper) } else { idx })
}
fn @repeat_lower[T](idx: i32, lower: i32, upper: i32) -> BoundaryMode[T] {
    BoundaryMode[T]::Index(if idx < lower { idx + (lower + upper) } else { idx })
}
fn @repeat_upper[T](idx: i32, lower: i32, upper: i32) -> BoundaryMode[T] {
    BoundaryMode[T]::Index(if idx >= upper { idx - (lower + upper) } else { idx })
}
fn @const_lower[T](idx: i32, lower: i32, _upper: i32, cval: T) -> BoundaryMode[T] {
    if idx < lower { BoundaryMode[T]::Const(cval) } else { BoundaryMode[T]::Index(idx) }
}
fn @const_upper[T](idx: i32, _lower: i32, upper: i32, cval: T) -> BoundaryMode[T] {
    if idx >= upper { BoundaryMode[T]::Const(cval) } else { BoundaryMode[T]::Index(idx) }
}
fn @wrap_const_fun[T](fun: fn(i32, i32, i32, T) -> BoundaryMode[T], cval: T) -> BoundaryFn[T] {
    @|idx, lower, upper| fun(idx, lower, upper, cval)
}

fn @map_boundary_mode[T](mode: BoundaryMode[T], bh_fun: fn(i32) -> BoundaryMode[T]) -> BoundaryMode[T] {
    match mode {
        BoundaryMode[T]::Index( idx) => bh_fun(idx),
        BoundaryMode[T]::Const(_val) => mode
    }
}

fn @read_pixel[T](x: i32, y: i32, region: Region, roi: RoI, read: fn(i32, i32) -> T, bh_lower: BoundaryFn[T], bh_upper: BoundaryFn[T]) -> T {
    let lower_x = roi.o_x;
    let lower_y = roi.o_y;
    let upper_x = roi.o_x + roi.width;
    let upper_y = roi.o_y + roi.height;

    let (mode_x, mode_y) = match region {
        (row, col) => {
            let mode_x = match row {
                Boundary::Lower   => bh_lower(roi.o_x + x, lower_x, upper_x),
                Boundary::Center  => BoundaryMode[T]::Index(roi.o_x + x),
                Boundary::Upper   => bh_upper(roi.o_x + x, lower_x, upper_x),
                Boundary::Unknown => map_boundary_mode[T](bh_lower(roi.o_x + x, lower_x, upper_x), @|idx| bh_upper(idx, lower_x, upper_x))
            };
            let mode_y = match col {
                Boundary::Lower   => bh_lower(roi.o_y + y, lower_y, upper_y),
                Boundary::Center  => BoundaryMode[T]::Index(roi.o_y + y),
                Boundary::Upper   => bh_upper(roi.o_y + y, lower_y, upper_y),
                Boundary::Unknown => map_boundary_mode[T](bh_lower(roi.o_y + y, lower_y, upper_y), @|idx| bh_upper(idx, lower_y, upper_y))
            };
            (mode_x, mode_y)
        }
    };

    match (mode_x, mode_y) {
        (BoundaryMode[T]::Index(x), BoundaryMode[T]::Index(y)) => read(x, y),
        (BoundaryMode[T]::Const(c), _) => c,
        (_, BoundaryMode[T]::Const(c)) => c
    }
}

// &            [T] on CPU/AVX
// &addrspace(1)[T] on CUDA/NVVM & OpenCL/AMDGPU
fn @get_acc_bh[T](img: Img, set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T, region: Region, bh_lower: BoundaryFn[T], bh_upper: BoundaryFn[T]) = get_acc_bh_offset[T](img, set_pixel, get_pixel, 0, 0, region, bh_lower, bh_upper)
fn @get_acc_bh_offset[T](img: Img, set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T, rox: i32, roy: i32, region: Region, bh_lower: BoundaryFn[T], bh_upper: BoundaryFn[T]) = Acc[T] {
    read  = @|x, y|    read_pixel[T](x + rox, y + roy, region, RoI { o_x = 0, o_y = 0, width = img.width, height = img.height }, @|x, y| get_pixel(y * img.stride + x), bh_lower, bh_upper),
    write = @|x, y, v| set_pixel((y + roy) * img.stride + x + rox, v)
}
fn @get_acc[T](img: Img, set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T) = get_acc_offset[T](img, set_pixel, get_pixel, 0, 0)
fn @get_acc_offset[T](img: Img, set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T, rox: i32, roy: i32) = Acc[T] {
    read  = @|x, y|    get_pixel((y + roy) * img.stride + x + rox),
    write = @|x, y, v| set_pixel((y + roy) * img.stride + x + rox, v)
}

// &            [T] on CPU/AVX
// &addrspace(3)[T] on CUDA/NVVM & OpenCL/AMDGPU
fn @get_acc_bh_memory[T](set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T, width: i32, height: i32, region: Region, bh_lower: BoundaryFn[T], bh_upper: BoundaryFn[T]) = get_acc_bh_offset_memory[T](set_pixel, get_pixel, width, height, 0, 0, region, bh_lower, bh_upper)
fn @get_acc_bh_offset_memory[T](set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T, width: i32, height: i32, rox: i32, roy: i32, region: Region, bh_lower: BoundaryFn[T], bh_upper: BoundaryFn[T]) = Acc[T] {
    read  = @|x, y|    read_pixel[T](x + rox, y + roy, region, RoI { o_x = 0, o_y = 0, width = width, height = height }, @|x, y| get_pixel(y * width + x), bh_lower, bh_upper),
    write = @|x, y, v| set_pixel((y + roy) * width + x + rox, v)
}
fn @get_acc_memory[T](set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T, width: i32, height: i32) = get_acc_offset_memory[T](set_pixel, get_pixel, width, height, 0, 0)
fn @get_acc_offset_memory[T](set_pixel: fn(i32, T) -> (), get_pixel: fn(i32) -> T, width: i32, _height: i32, rox: i32, roy: i32) = Acc[T] {
    read  = @|x, y|    get_pixel((y + roy) * width + x + rox),
    write = @|x, y, v| set_pixel((y + roy) * width + x + rox, v)
}

fn @new_img(width: i32, height: i32, stride: i32, buf: Buffer) = Img {
    buf = buf, width = width, height = height, stride = stride
}

fn @alloc_img[T](img: Img, alloc: fn(i64) -> Buffer) = new_img(img.width, img.height, img.stride, alloc((img.stride * img.height) as i64 * sizeof[T]()))

fn @create_img[T](width: i32, height: i32, alloc: fn(i64) -> Buffer) -> Img {
    let stride = round_up(width * sizeof[T]() as i32, 64) / sizeof[T]() as i32;
    new_img(width, height, stride, alloc((stride * height) as i64 * sizeof[T]()))
}

fn @copy_img(src: Img, dst: Img) = copy(src.buf, dst.buf)

fn @get_mask3(data : [[f32 * 3] * 3]) = Mask {
    data = @|x, y| -> f32 { data(y)(x) },
    size_x = 3, size_y = 3
}
fn @get_mask5(data : [[f32 * 5] * 5]) = Mask {
    data = @|x, y| -> f32 { data(y)(x) },
    size_x = 5, size_y = 5
}
fn @get_mask_sep3(data : [f32 * 3]) = MaskSep {
    data = @|x| -> f32 { data(x) },
    size = 3
}
fn @get_mask_sep5(data : [f32 * 5]) = MaskSep {
    data = @|x| -> f32 { data(x) },
    size = 5
}

// image and accessor lists

struct ImgList {
    get : fn(i32) -> Img,
    ext : fn(i32) -> (i32, i32),
    size : i32, id : i32
}

fn @img_list_empty() = ImgList {
    get = @|_i| undef[Img](),
    ext = @|_i| undef[(i32, i32)](),
    size = 0, id = 0
}

fn @img_list_tail(img: Img, ext: (i32, i32), n: i32) = ImgList {
    get = @|i| { if i == n - 1 { img } else { undef[Img]() } },
    ext = @|i| { if i == n - 1 { ext } else { undef[(i32, i32)]() } },
    size = n, id = n
}

fn @img_list_prepend(list: ImgList, img: Img, ext: (i32, i32)) = ImgList {
    get = @|i| { if i == list.id - 2 { img } else { list.get(i) } },
    ext = @|i| { if i == list.id - 2 { ext } else { list.ext(i) } },
    size = list.size, id = list.id - 1
}

fn @make_img_list1(img: Img, ext: (i32, i32)) = img_list_tail(img, ext, 1)
fn @make_img_list2(img1: Img, ext1: (i32, i32), img2: Img, ext2: (i32, i32)) = img_list_prepend(img_list_tail(img2, ext2, 2), img1, ext1)
fn @make_img_list3(img1: Img, ext1: (i32, i32), img2: Img, ext2: (i32, i32), img3: Img, ext3: (i32, i32)) = img_list_prepend(img_list_prepend(img_list_tail(img3, ext3, 3), img2, ext2), img1, ext1)

fn @img_list_map(body: fn(Img, (i32, i32)) -> Img) = @|imgs: ImgList| {
    fn @(?i)map(list: ImgList, i: i32) -> ImgList {
        if i == 0 {
            list
        } else if i == imgs.size {
            map(img_list_tail(@body(imgs.get(i - 1), imgs.ext(i - 1)), imgs.ext(i - 1), imgs.size), i - 1)
        } else {
            map(img_list_prepend(list, @body(imgs.get(i - 1), imgs.ext(i - 1)), imgs.ext(i - 1)), i - 1)
        }
    }

    map(img_list_empty(), imgs.size)
}

fn @img_list2acc_map[T](body: fn(Img, (i32, i32)) -> Acc[T]) = @|imgs: ImgList| {
    fn @(?i)map(list: AccList[T], i: i32) -> AccList[T] {
        if i == 0 {
            list
        } else if i == imgs.size {
            map(acc_list_tail[T](@body(imgs.get(i - 1), imgs.ext(i - 1)), imgs.size), i - 1)
        } else {
            map(acc_list_prepend[T](list, @body(imgs.get(i - 1), imgs.ext(i - 1))), i - 1)
        }
    }

    map(acc_list_empty[T](), imgs.size)
}

fn @get_max_ext(imgs: ImgList) -> (i32, i32) {
    fn @(?i)max_ext(ext as (ext_x: i32, ext_y: i32), i: i32) -> (i32, i32) {
        if i == 0 {
            ext
        } else {
            let (img_ext_x, img_ext_y) = imgs.ext(i - 1);
            max_ext((max(ext_x, img_ext_x), max(ext_y, img_ext_y)), i - 1)
        }
    }

    max_ext((0, 0), imgs.size)
}


struct AccList[T] {
    get : fn(i32) -> Acc[T],
    size : i32, id : i32
}

fn @acc_list_empty[T]() = AccList[T] {
    get = @|_i| { undef[Acc[T]]() },
    size = 0, id = 0
}

fn @acc_list_tail[T](acc: Acc[T], n: i32) = AccList[T] {
    get = @|i| { if i == n - 1 { acc } else { undef[Acc[T]]() } },
    size = n, id = n
}

fn @acc_list_prepend[T](list: AccList[T], acc: Acc[T]) = AccList[T] {
    get = @|i| { if i == list.id - 2 { acc } else { list.get(i) } },
    size = list.size, id = list.id - 1
}

fn @make_acc_list1[T](acc: Acc[T]) = acc_list_tail[T](acc, 1)
fn @make_acc_list2[T](acc1: Acc[T], acc2: Acc[T]) = acc_list_prepend[T](acc_list_tail[T](acc2, 2), acc1)
fn @make_acc_list3[T](acc1: Acc[T], acc2: Acc[T], acc3: Acc[T]) = acc_list_prepend[T](acc_list_prepend[T](acc_list_tail[T](acc3, 3), acc2), acc1)
