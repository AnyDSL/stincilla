static Region2D    = [ 0, 1, 2,
                       3, 4, 5,
                       6, 7, 8 ];
static Region2DCol = [ 1, 4, 7 ];
static Region2DRow = [ 3, 4, 5 ];

fn get_device_copy(img: Img) -> Img { img }
fn release_device_copy(img: Img) -> () { }

fn set_pixel_fn(img: Img) -> fn(i32, f32) -> () { |idx, val| bitcast[&mut[f32]](img.buf.data)(idx) = val }
fn get_pixel_fn(img: Img) -> fn(i32) -> f32 { |idx| bitcast[&[f32]](img.buf.data)(idx) }
fn set_pixel_memory_fn(buf: Buffer) -> fn(i32, f32) -> () { |idx, val| bitcast[&mut[f32]](buf.data)(idx) = val }
fn get_pixel_memory_fn(buf: Buffer) -> fn(i32) -> f32 { |idx| bitcast[&[f32]](buf.data)(idx) }

fn iteration_point(out: Img, body: fn(i32, i32, Acc) -> ()) -> () {
    let out_acc = get_acc(out, set_pixel_fn(out), get_pixel_fn(out));

    for benchmark_cpu() {
        for y in $outer_loop(0, out.height) {
            for x in inner_loop(0, out.width) @{
                body(x, y, out_acc);
            }
        }
    }
}

fn iteration(out: Img, arr: Img, mask: Mask, bh_lower: BoundaryFn, bh_upper: BoundaryFn, body: StencilFn) -> () {
    let out_acc = get_acc(out, set_pixel_fn(out), get_pixel_fn(out));
    let arr_acc = get_acc_bh(arr, set_pixel_fn(arr), get_pixel_fn(arr), 10, bh_lower, bh_upper);

    for benchmark_cpu() {
        for y in $outer_loop(0, out.height) {
            for x in inner_loop(0, out.width) @{
                body(x, y, out_acc, arr_acc, mask);
            }
        }
    }
}

fn iteration_bounds(out: Img, arr: Img, mask: Mask, bh_lower: BoundaryFn, bh_upper: BoundaryFn, body: StencilFn) -> () {
    // compute the number of vector strides for boundary handling
    let bhy = mask.size_y / 2;
    let bhx = round_up(mask.size_x / 2, get_vector_length());

    // (minx, maxx, miny, maxy)
    let width  = out.width;
    let height = out.height;
    let Bounds2D = [
        (0          , bhx        , 0           , bhy),
        (bhx        , width - bhx, 0           , bhy),
        (width - bhx, width      , 0           , bhy),

        (0          , bhx        , bhy         , height - bhy),
        (bhx        , width - bhx, bhy         , height - bhy),
        (width - bhx, width      , bhy         , height - bhy),

        (0          , bhx        , height - bhy, height),
        (bhx        , width - bhx, height - bhy, height),
        (width - bhx, width      , height - bhy, height)
    ];

    for benchmark_cpu() {
        for iter in @range(0, 9) {
            let bounds = Bounds2D(iter);
            let region = Region2D(iter);
            let arr_acc = get_acc_bh(arr, set_pixel_fn(arr), get_pixel_fn(arr), region, bh_lower, bh_upper);
            let out_acc = get_acc(out, set_pixel_fn(out), get_pixel_fn(out));
            //let outer_loop_fun = if region == 4 { outer_loop } else { range };
            // gets slower when computed in parallel

            for y in $range(bounds(2), bounds(3)) {
                for x in inner_loop(bounds(0), bounds(1)) @{
                    body(x, y, out_acc, arr_acc, mask);
                }
            }
        }
    }
}

struct CacheSizes {
    sizes : [i32 * 3],
    count : i32,
}

fn tiled_loop(xl: i32, xu: i32, yl: i32, yu: i32, mask: Mask, img: Img, body: fn(i32, i32) -> ()) -> () {
    fn get_tile_dims(mask: Mask, img: Img) -> (i32, i32) {
        // TODO make this dimension independent, currently only 2D is supported
        // TODO get cache information from machine/compiler/somewhere else

        let caches = CacheSizes {sizes: [32*1024, 256*1024, 20*1024*1024], count: 3}; // in bytes
        let element_size = sizeof[i32](); // TODO make this dynamic based on used type. templates?
        
        //print_string("img: (");
        //print_int(img.width);
        //print_string(", ");
        //print_int(img.height);
        //print_string(" : ");
        //print_int(img.stride);
        //print_string(")\n");
        //
        //print_string("mask: (");
        //print_int(mask.size_x);
        //print_string(", ");
        //print_int(mask.size_y);
        //print_string(")\n");
        
        let mut inner_tile_size = img.width;
        for i in range(0, caches.count) {
            // This is a worst-case model, correct for box stencils, pessimistic for all others
            // TODO take detailed shape into account
            let cache_blocked = ((caches.sizes(i) / (mask.size_y*element_size) / 100 * 90)>>6)<<6;
            if cache_blocked < img.width && cache_blocked > 200 {
                inner_tile_size = cache_blocked;
            }
            
            //print_int(cache_sizes(i));
            //print_string(" <= ");
            //print_int(img.width*mask.size_y*element_size);
            //if cache_sizes(i) <= img.width*mask.size_y*element_size {
            //    print_string(" = false; ");
            //} else {
            //    print_string(" = true; ");
            //}
        }
        //print_string("\nRecommended inner tiling: ");
        //print_int(inner_tile_size);
        //print_string("\n");
        
        (inner_tile_size, img.height)
    }
    
    let x_upper = xu;
    let x_lower = xl;
    let y_upper = yu;
    let y_lower = yl;

    fn tile(cur_lvl: i32, xl: i32, xu: i32, yl: i32, yu: i32) -> () {
        //print_string("tile(");
        //print_int(cur_lvl);
        //print_string(", ");
        //print_int(xl);
        //print_string(", ");
        //print_int(xu);
        //print_string(", ");
        //print_int(yl);
        //print_string(", ");
        //print_int(yu);
        //print_string(")\n");
     
        let (xtile_dim, ytile_dim) = @get_tile_dims(mask, img);
        if cur_lvl == 0 {
             // we always start from 0 to align tiles, no matter what boundary is skipped over
            for x in $range_step(0, xu, xtile_dim) {
                for y in $range_step(0, yu, ytile_dim) @{
                    tile(cur_lvl + 1,
                         max(x, xl), min(x + xtile_dim, xu),
                         max(y, yl), min(y + ytile_dim, yu));
                }
            }
        } else {
            for y in range(yl, yu) {
                for x in range(xl, xu) @{
                    body(x, y);
                }
            }
        }
    }

    tile(0, xl, xu, yl, yu)
}

fn iteration_advanced(out: Img, arr: Img, mask: Mask, bh_lower: BoundaryFn, bh_upper: BoundaryFn, body: StencilFn) -> () {
    // compute the number of vector strides for boundary handling
    let bhy = mask.size_y / 2;
    let bhx = round_up(mask.size_x / 2, get_vector_length());

    // (minx, maxx, miny, maxy)
    let width  = out.width;
    let height = out.height;
    let Bounds2D = [
        (0          , bhx        , 0           , bhy),
        (bhx        , width - bhx, 0           , bhy),
        (width - bhx, width      , 0           , bhy),

        (0          , bhx        , bhy         , height - bhy),
        (bhx        , width - bhx, bhy         , height - bhy),
        (width - bhx, width      , bhy         , height - bhy),

        (0          , bhx        , height - bhy, height),
        (bhx        , width - bhx, height - bhy, height),
        (width - bhx, width      , height - bhy, height)
    ];

    for benchmark_cpu() {
        for iter in @range(0, 9) {
            let bounds = Bounds2D(iter);
            let region = Region2D(iter);
            let arr_acc = get_acc_bh(arr, set_pixel_fn(arr), get_pixel_fn(arr), region, bh_lower, bh_upper);
            let out_acc = get_acc(out, set_pixel_fn(out), get_pixel_fn(out));
            //let outer_loop_fun = if region == 4 { outer_loop } else { range };
            // gets slower when computed in parallel

            if region == 4 {
                @tiled_loop(bounds(0), bounds(1), bounds(2), bounds(3), mask, arr, |x, y| body(x, y, out_acc, arr_acc, mask));
            } else {
                for y in $range(bounds(2), bounds(3)) {
                    for x in inner_loop(bounds(0), bounds(1)) @{
                        body(x, y, out_acc, arr_acc, mask);
                    }
                }
            }
        }
    }
}

fn iteration_sep(out: Img, arr: Img, mask_row: MaskSep, mask_col: MaskSep, bh_lower: BoundaryFn, bh_upper: BoundaryFn, body: StencilSepFn) -> () {
    // allocate temporary array
    let tmp = get_img(alloc_cpu(out.width * out.height * sizeof[f32]()), out.width, out.height);
    let out_acc   = get_acc(out, set_pixel_fn(out), get_pixel_fn(out));
    let tmp_acc_w = get_acc(tmp, set_pixel_fn(tmp), get_pixel_fn(tmp));
    let tmp_acc_r = get_acc_bh(tmp, set_pixel_fn(tmp), get_pixel_fn(tmp), 10, bh_lower, bh_upper);
    let arr_acc   = get_acc_bh(arr, set_pixel_fn(arr), get_pixel_fn(arr), 10, bh_lower, bh_upper);

    for benchmark_cpu() {
        for y in $outer_loop(0, out.height) {
            for x in inner_loop(0, out.width) @{
                let is_row = false;
                body(x, y, tmp_acc_w, arr_acc, mask_col, is_row);
            }
        }

        for y in $outer_loop(0, out.height) {
            for x in inner_loop(0, out.width) @{
                let is_row = true;
                body(x, y, out_acc, tmp_acc_r, mask_row, is_row);
            }
        }
    }

    release(tmp.buf);
}

fn iteration_sep_bounds(out: Img, arr: Img, mask_row: MaskSep, mask_col: MaskSep, bh_lower: BoundaryFn, bh_upper: BoundaryFn, body: StencilSepFn) -> () {
    // allocate temporary array
    let tmp = get_img(alloc_cpu(out.width * out.height * sizeof[f32]()), out.width, out.height);

    let bhy = mask_col.size / 2;
    let bhx = round_up(mask_row.size / 2, get_vector_length());

    let Bounds2DCol = [
        (0               , bhy),
        (bhy             , out.height - bhy),
        (out.height - bhy, out.height)
    ];

    let Bounds2DRow = [
        (0              , bhx),
        (bhx            , out.width - bhx),
        (out.width - bhx, out.width)
    ];

    for benchmark_cpu() {
        for iter in @range(0, 3) {
            let bounds = Bounds2DCol(iter);
            let region = Region2DCol(iter);
            let arr_acc = get_acc_bh(arr, set_pixel_fn(arr), get_pixel_fn(arr), region, bh_lower, bh_upper);
            let tmp_acc = get_acc(tmp, set_pixel_fn(tmp), get_pixel_fn(tmp));
            let outer_loop_fun = if region == 4 { outer_loop } else { range };
            for y in $outer_loop_fun(bounds(0), bounds(1)) {
                for x in inner_loop(0, out.width) @{
                    let is_row = false;
                    body(x, y, tmp_acc, arr_acc, mask_col, is_row);
                }
            }
        }

        for iter in @range(0, 3) {
            let bounds = Bounds2DRow(iter);
            let region = Region2DRow(iter);
            let tmp_acc = get_acc_bh(tmp, set_pixel_fn(tmp), get_pixel_fn(tmp), region, bh_lower, bh_upper);
            let out_acc = get_acc(out, set_pixel_fn(out), get_pixel_fn(out));
            let outer_loop_fun = if region == 4 { outer_loop } else { range };
            for y in $outer_loop_fun(0, out.height) {
                for x in inner_loop(bounds(0), bounds(1)) @{
                    let is_row = true;
                    body(x, y, out_acc, tmp_acc, mask_row, is_row);
                }
            }
        }
    }

    release(tmp.buf);
}

fn iteration_sep_advanced(out: Img, arr: Img, mask_row: MaskSep, mask_col: MaskSep, bh_lower: BoundaryFn, bh_upper: BoundaryFn, body: StencilSepFn) -> () {
    let unroll = 1;
    let bhy = round_up(mask_col.size / 2, unroll);
    let bhx = round_up(mask_row.size / 2, get_vector_length());

    let Bounds2DCol = [
        (0               , bhy),
        (bhy             , out.height - bhy),
        (out.height - bhy, out.height)
    ];

    let Bounds2DRow = [
        (0              , bhx),
        (bhx            , out.width - bhx),
        (out.width - bhx, out.width)
    ];

    for benchmark_cpu() {
        for iter_col in @range(0, 3) {
            let bounds_col = Bounds2DCol(iter_col);
            let region_col = Region2DCol(iter_col);
            let outer_col  = if iter_col == 1 { outer_loop_step } else { range_step };
            let inner_col  = if iter_col == 1 { inner_loop } else { range };
            for y in $outer_col(bounds_col(0), bounds_col(1), unroll) @{
                // allocate temporary array per thread
                let tmp = alloc_cpu(out.width * unroll * sizeof[f32]());

                for x in $inner_col(0, out.width) @{
                    let is_row = false;

                    // index space: cache line
                    let arr_acc = get_acc_bh_offset(arr, set_pixel_fn(arr), get_pixel_fn(arr), 0, y, region_col, bh_lower, bh_upper);
                    let tmp_acc = get_acc_memory(set_pixel_memory_fn(tmp), get_pixel_memory_fn(tmp), out.width, unroll);
                    for i in range(0, unroll) {
                        body(x, i, tmp_acc, arr_acc, mask_col, is_row);
                    }
                }
                for iter_row in range(0, 3) {
                    let bounds_row = Bounds2DRow(iter_row);
                    let region_row = Region2DRow(iter_row);
                    let inner_row  = if iter_row == 1 { inner_loop } else { range };
                    for x in $inner_row(bounds_row(0), bounds_row(1)) @{
                        let is_row = true;

                        // index space: cache line
                        let tmp_acc = get_acc_bh_memory(set_pixel_memory_fn(tmp), get_pixel_memory_fn(tmp), out.width, unroll, region_row, bh_lower, bh_upper);
                        let out_acc = get_acc_offset(out, set_pixel_fn(out), get_pixel_fn(out), 0, y);
                        for i in range(0, unroll) {
                            body(x, i, out_acc, tmp_acc, mask_row, is_row);
                        }
                    }
                }
                release(tmp);
            }
        }
    }
}
