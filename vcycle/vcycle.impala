// Deriv 2D
fn @deriv2D(RHS: Img, img: Img) -> () {
    let mask = get_mask3([[0.0f,  1.0f, 0.0f],
                          [1.0f, -4.0f, 1.0f],
                          [0.0f,  1.0f, 0.0f]]);
    for math, x, y, rhs_acc, img_acc, mask in iteration_bounds(math, RHS, img, mask, mirror_lower, mirror_upper) {
        rhs_acc.write(x, y, apply_stencil(x, y, img_acc, mask));
    }
}


// Jacobi
static omega = 0.8f;
static jacobi_mask = Mask {
    data : @|x: i32, y: i32| -> f32 { [[0.00f, 0.25f, 0.00f],
                                       [0.25f, 0.00f, 0.25f],
                                       [0.00f, 0.25f, 0.00f]](y)(x) },
    size_x : 3, size_y : 3
};
// Dirichlet boundary handling -> 0
//static dirichlet_lower = wrap_const_fun(const_lower, 0.0f);
//static dirichlet_upper = wrap_const_fun(const_upper, 0.0f);
fn @dirichlet_lower(idx: i32, lower: i32, upper: i32) -> BoundaryMode { const_lower(idx, lower, upper, 0.0f) }
fn @dirichlet_upper(idx: i32, lower: i32, upper: i32) -> BoundaryMode { const_upper(idx, lower, upper, 0.0f) }
fn @jacobi_solver(math: Intrinsics, x: i32, y: i32, Sol: Acc, Tmp: Acc, RHS: Acc, mask: Mask) -> () {
    let val = (1.0f - omega) * Tmp.read(x, y) +
              0.25f * omega  * RHS.read(x, y) + apply_stencil(x, y, Tmp, mask);
    Sol.write(x, y, val);
}

fn @jacobi(Sol: Img, Tmp: Img, RHS: Img, omega: f32) -> () {
    for math, x, y, Sol, Tmp, mask in iteration_bounds(math, Sol, Tmp, jacobi_mask, dirichlet_lower, dirichlet_upper) {
        let RHS = get_acc(RHS, set_pixel_fn(RHS), get_pixel_fn(RHS));
        let val = (1.0f - omega) * Tmp.read(x, y) +
                  0.25f * omega  * RHS.read(x, y) + apply_stencil(x, y, Tmp, mask);
        Sol.write(x, y, val);
    }
}


// Residual
static residual_mask = Mask {
    data : @|x: i32, y: i32| -> f32 { [[0.0f,  1.0f, 0.0f],
                                       [1.0f, -4.0f, 1.0f],
                                       [0.0f,  1.0f, 0.0f]](y)(x) },
    size_x : 3, size_y : 3
};
fn @residual_fun(math: Intrinsics, x: i32, y: i32, Res: Acc, Sol: Acc, RHS: Acc, mask: Mask) -> () {
    let val = RHS.read(x, y) + apply_stencil(x, y, Sol, mask);
    Res.write(x, y, val);
}

fn @residual(math: Intrinsics, Res: Img, Sol: Img, RHS: Img) -> () {
    for math, x, y, Res, sol_acc, mask in iteration_bounds(math, Res, Sol, residual_mask, dirichlet_lower, dirichlet_upper) {
        let RHS = get_acc(RHS, set_pixel_fn(RHS), get_pixel_fn(RHS));
        let val = RHS.read(x, y) + apply_stencil(x, y, sol_acc, mask);
        Res.write(x, y, val);
    }
}


// Restrict: arr = size x size; out = size/2 x size/2; fine -> coarse
// coarse = Stencil * fine;
static restrict_mask = Mask {
    data : @|x: i32, y: i32| -> f32 { [[0.25f, 0.50f, 0.25f],
                                       [0.50f, 1.00f, 0.50f],
                                       [0.25f, 0.50f, 0.25f]](y)(x) },
    size_x : 3, size_y : 3
};
fn @restrict_fun(math: Intrinsics, x: i32, y: i32, RHS: Acc, Res: Acc, mask: Mask) -> () {
    let val = Res.read(2*x,   2*y  ) +
              Res.read(2*x+1, 2*y  ) +
              Res.read(2*x,   2*y+1) +
              Res.read(2*x+1, 2*y+1);
    RHS.write(x, y, val);
}

fn @restrict(math: Intrinsics, RHS: Img, Res: Img) -> () {
    for math, x, y, RHS, Res, mask in iteration(math, RHS, Res, restrict_mask, nobh_center, nobh_center) {
        let val = Res.read(2*x,   2*y  ) +
                  Res.read(2*x+1, 2*y  ) +
                  Res.read(2*x,   2*y+1) +
                  Res.read(2*x+1, 2*y+1);
        RHS.write(x, y, val);
    }
}


// Interpolate: arr = size/2 x size/2; out = size x size; coarse -> fine
// fine += Stencil * coarse:
static interpolate_mask = Mask {
    data : @|x: i32, y: i32| -> f32 { [[0.25f, 0.50f, 0.25f],
                                       [0.50f, 1.00f, 0.50f],
                                       [0.25f, 0.50f, 0.25f]](y)(x) },
    size_x : 3, size_y : 3
};
fn @interpolate_fun(math: Intrinsics, x: i32, y: i32, SolF: Acc, SolC: Acc, mask: Mask) -> () {
    let val = SolC.read(x, y);
    SolF.write(2*x  , 2*y  , SolF.read(2*x  , 2*y  ) + val);
    SolF.write(2*x+1, 2*y  , SolF.read(2*x+1, 2*y  ) + val);
    SolF.write(2*x  , 2*y+1, SolF.read(2*x  , 2*y+1) + val);
    SolF.write(2*x+1, 2*y+1, SolF.read(2*x+1, 2*y+1) + val);
}

fn @interpolate(SolF: Img, SolC: Img) -> () {
    // use iteration space of SolC, compute 4x elements
    for math, x, y, SolC, SolF, mask in iteration(math, SolC, SolF, interpolate_mask, nobh_center, nobh_center) {
        let val = SolC.read(x, y);
        SolF.write(2*x  , 2*y  , SolF.read(2*x  , 2*y  ) + val);
        SolF.write(2*x+1, 2*y  , SolF.read(2*x+1, 2*y  ) + val);
        SolF.write(2*x  , 2*y+1, SolF.read(2*x  , 2*y+1) + val);
        SolF.write(2*x+1, 2*y+1, SolF.read(2*x+1, 2*y+1) + val);
    }
}


// L2 norm
fn @l2norm(img: Img) -> f32 {
    // TODO: runs only on the CPU atm
    let mut l2sum = 0.0f;
    for y in range(0, img.height) {
        for x in range(0, img.width) {
            let value = bitcast[&[f32]](img.buf.data)(y * img.stride + x);
            l2sum += value*value;
        }
    }
    cpu_intrinsics.sqrtf(l2sum) / (img.width*img.height) as f32
}


// normal V-Cycle, components are hard-coded
fn vcycle_reference(RHS0: Img, levels: i32, vsteps: i32, ssteps: i32) -> Img {
    let width  = RHS0.width;
    let height = RHS0.height;
    let RHS = ~[levels:Img];
    let Sol = ~[levels:Img];
    let Tmp = ~[levels:Img];
    let Res = ~[levels:Img];
    for level in range(0, levels) {
        fn init_img(level: i32) -> Img { create_img(width >> level, height >> level, alloc_cpu) }
        RHS(level) = if level == 0 { RHS0 } else { init_img(level) };
        Sol(level) = init_img(level);
        Res(level) = init_img(level);
        Tmp(level) = init_img(level);
        init_zero(bitcast[&mut[f32]](Tmp(level).buf.data), Tmp(level).stride, Tmp(level).height);
    }

    fn vcycle_intern(level: i32) -> () {
        if level == levels-1 {
            // smooth
            for i in range(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                jacobi(Sol(level), Tmp(level), RHS(level), omega);
            }
        } else {
            // pre-smooth
            for i in range(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                jacobi(Sol(level), Tmp(level), RHS(level), omega);
            }

            residual(math, Res(level), Sol(level), RHS(level));
            restrict(math, RHS(level+1), Res(level));

            //if level == 0 { print_string("L2 norm: "); print_f32(l2norm(Res(level))); print_char('\n'); }

            init_zero(bitcast[&mut[f32]](Tmp(level+1).buf.data), Tmp(level+1).stride, Tmp(level+1).height);
            vcycle_intern(level+1);

            interpolate(Sol(level), Sol(level+1));

            // post-smooth
            for i in range(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                jacobi(Sol(level), Tmp(level), RHS(level), omega);
            }
        }
    }
    for i in range(0, vsteps) {
        vcycle_intern(0);
    }

    let result = Sol(0);

    for level in range(0, levels) {
        if level != 0 {
            release(RHS(level).buf);
            release(Sol(level).buf);
        }
        release(Res(level).buf);
        release(Tmp(level).buf);
    }

    runtime_release(0, bitcast[&[i8]](RHS));
    runtime_release(0, bitcast[&[i8]](Sol));
    runtime_release(0, bitcast[&[i8]](Tmp));
    runtime_release(0, bitcast[&[i8]](Res));

    result
}


// V-Cycle DSL: solver, etc. can be provided by the user
fn @vcycle_dsl(RHS0: Img, levels: i32, vsteps: i32, ssteps: i32,
              solver: fn(Intrinsics, i32, i32, Acc, Acc, Acc, Mask) -> (), solver_mask: Mask,
              residual: fn(Intrinsics, i32, i32, Acc, Acc, Acc, Mask) -> (), residual_mask: Mask,
              restrict: fn(Intrinsics, i32, i32, Acc, Acc, Mask) -> (), restrict_mask: Mask,
              interpolate: fn(Intrinsics, i32, i32, Acc, Acc, Mask) -> (), interpolate_mask: Mask
             ) -> Img {
    let width  = RHS0.width;
    let height = RHS0.height;
    let RHS = ~[levels:Img];
    let Sol = ~[levels:Img];
    let Tmp = ~[levels:Img];
    let Res = ~[levels:Img];
    for level in unroll(0, levels) {
        fn init_img(level: i32) -> Img { create_img(width >> level, height >> level, alloc_cpu) }
        RHS(level) = if level == 0 { RHS0 } else { init_img(level) };
        Sol(level) = init_img(level);
        Res(level) = init_img(level);
        Tmp(level) = init_img(level);
        init_zero(bitcast[&mut[f32]](Tmp(level).buf.data), Tmp(level).stride, Tmp(level).height);
    }

    fn @(?level) vcycle_dsl_intern(level: i32) -> () {
        let rhs_level = RHS(level);
        let rhs_level_dev = get_device_copy(rhs_level);
        if level == levels-1 {
            // smooth
            for i in unroll(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                for math, x, y, sol_acc, tmp_acc, mask in iteration_bounds(math, Sol(level), Tmp(level), solver_mask, dirichlet_lower, dirichlet_upper) {
                    let rhs_acc = get_acc(rhs_level_dev, set_pixel_fn(rhs_level_dev), get_pixel_fn(rhs_level_dev));
                    solver(math, x, y, sol_acc, tmp_acc, rhs_acc, mask);
                }
            }
        } else {
            // pre-smooth
            for i in unroll(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                for math, x, y, sol_acc, tmp_acc, mask in iteration_bounds(math, Sol(level), Tmp(level), solver_mask, dirichlet_lower, dirichlet_upper) {
                    let rhs_acc = get_acc(rhs_level_dev, set_pixel_fn(rhs_level_dev), get_pixel_fn(rhs_level_dev));
                    solver(math, x, y, sol_acc, tmp_acc, rhs_acc, mask);
                }
            }

            let merge = true;
            if merge == true {
                iteration_merge(math, RHS(level+1), Sol(level), RHS(level), dirichlet_lower, dirichlet_upper, residual, residual_mask, restrict, restrict_mask);
            } else {
                for math, x, y, res_acc, sol_acc, mask in iteration_bounds(math, Res(level), Sol(level), residual_mask, dirichlet_lower, dirichlet_upper) {
                    let rhs_acc = get_acc(rhs_level_dev, set_pixel_fn(rhs_level_dev), get_pixel_fn(rhs_level_dev));
                    residual(math, x, y, res_acc, sol_acc, rhs_acc, mask);
                }

                for math, x, y, rhs_acc, res_acc, mask in iteration(math, RHS(level+1), Res(level), residual_mask, nobh_center, nobh_center) {
                    restrict(math, x, y, rhs_acc, res_acc, mask);
                }
            }

            //if level == 0 { print_string("L2 norm: "); print_f32(l2norm(Res(level))); print_char('\n'); }

            init_zero(bitcast[&mut[f32]](Tmp(level+1).buf.data), Tmp(level+1).stride, Tmp(level+1).height);
            vcycle_dsl_intern(level+1);

            // use iteration space of SolC, compute 4x elements
            for math, x, y, solc_acc, solf_acc, mask in iteration(math, Sol(level+1), Sol(level), interpolate_mask, nobh_center, nobh_center) {
                interpolate(math, x, y, solf_acc, solc_acc, mask);
            }

            // post-smooth
            for i in unroll(0, ssteps) {
                if i>0 { let tmp = Sol(level); Sol(level) = Tmp(level); Tmp(level) = tmp; }
                for math, x, y, sol_acc, tmp_acc, mask in iteration_bounds(math, Sol(level), Tmp(level), solver_mask, dirichlet_lower, dirichlet_upper) {
                    let rhs_acc = get_acc(rhs_level_dev, set_pixel_fn(rhs_level_dev), get_pixel_fn(rhs_level_dev));
                    solver(math, x, y, sol_acc, tmp_acc, rhs_acc, mask);
                }
            }
        }
        release_device_copy(rhs_level_dev);
    }
    for i in unroll(0, vsteps) {
        vcycle_dsl_intern(0);
    }

    let result = Sol(0);

    for level in unroll(0, levels) {
        if level != 0 {
            release(RHS(level).buf);
            release(Sol(level).buf);
        }
        release(Res(level).buf);
        release(Tmp(level).buf);
    }

    runtime_release(0, bitcast[&[i8]](RHS));
    runtime_release(0, bitcast[&[i8]](Sol));
    runtime_release(0, bitcast[&[i8]](Tmp));
    runtime_release(0, bitcast[&[i8]](Res));

    result
}


extern
fn vcycle(img_data: &[f32], out_data: &[f32], width: i32, height: i32) -> () {
    let img = Img { buf : Buffer { data : bitcast[&[i8]](img_data), size : (width * height * sizeof[f32]()) as i64, device : 0 }, width : width, height : height, stride : width };
    let out = Img { buf : Buffer { data : bitcast[&[i8]](out_data), size : (width * height * sizeof[f32]()) as i64, device : 0 }, width : width, height : height, stride : width };
    let RHS = create_img(width, height, alloc_cpu);

    deriv2D(RHS, img);

    let levels = 3;
    let vsteps = 1;
    let ssteps = 2;

    let result = vcycle_dsl(RHS, levels, vsteps, ssteps, jacobi_solver, jacobi_mask, residual_fun, residual_mask, restrict_fun, restrict_mask, interpolate_fun, interpolate_mask);
    //let result = vcycle_reference(RHS, levels, vsteps, ssteps);
    print_total_timing();

    for y in range(0, out.height) {
        for x in range(0, out.width) {
            let out_acc = get_acc(out,    @|idx, val| { bitcast[&mut[f32]](out.buf.data)(idx) = val },    @|idx| bitcast[&[f32]](out.buf.data)(idx));
            let res_acc = get_acc(result, @|idx, val| { bitcast[&mut[f32]](result.buf.data)(idx) = val }, @|idx| bitcast[&[f32]](result.buf.data)(idx));
            out_acc.write(x, y, res_acc.read(x, y));
        }
    }

    release(result.buf);
    release(RHS.buf);
}
