type Mask = Mask3;
type MaskSep = Mask3Sep;

fn apply_stencil(x: i32, y: i32, acc: Acc, stencil: Mask) -> f32 {
    let mut sum = 0.0f;
    let half_range_y = stencil.size_y / 2;
    let half_range_x = stencil.size_x / 2;

    for j in range(-half_range_y, half_range_y+1) {
        for i in range(-half_range_x, half_range_x+1) {
            let stencil_val = stencil.data(j + half_range_y)(i + half_range_x);
            if stencil_val != 0.0f {
                sum += acc.read(x + i, y + j) * stencil_val;
            } else {
                // additional logic for empty stencil value
            }
        }
    }

    sum
}

fn main() -> i32 {
    let width  = 2048;
    let height = 2048;
    let a      = 0.2f;
    let b      = 1.0f - 4.0f * a;
    let arr    = Img { buf : alloc_cpu(width * height * sizeof[f32]()), width : width, height : height, stride : width };
    let out    = Img { buf : alloc_cpu(width * height * sizeof[f32]()), width : width, height : height, stride : width };
    init_rand(bitcast[&[f32]](arr.buf.data), arr.stride, out.height);
    init_zero(bitcast[&[f32]](out.buf.data), out.stride, out.height);

    let stencil = Mask {
        data : [[0.0f, b, 0.0f],
                [   b, a, b   ],
                [0.0f, b, 0.0f]],
        size_x : 3, size_y : 3
    };

    let lower = clamp_lower;
    let upper = clamp_upper;

    let iteration_fun = iteration;                  // SS
    //let iteration_fun = iteration_bounds;           // SS + BH
    //let iteration_fun = iteration_advanced;         // SS + SM

    for x, y, out_acc, arr_acc, stencil in @iteration_fun(out, arr, stencil, lower, upper) {
        out_acc.write(x, y, apply_stencil(x, y, arr_acc, stencil));
    }
    print_total_timing();

    fn reference() -> i32 {
        let mut passed = 0;
        let arr_acc = get_acc_bh(arr, 10, lower, upper);
        let out_acc = get_acc(out);
        for y in $range(0, out.height) {
            for x in range(0, out.width) @{
                let ref = apply_stencil(x, y, arr_acc, stencil);
                let mut diff = ref - out_acc.read(x, y);
                if diff < 0.0f  { diff = -diff; }
                if diff > 0.01f { passed = 42; }
            }
        }
        if passed == 0 {
            print_string("Test PASSED!\n");
        } else {
            print_string("Test FAILED!\n");
        }
        passed
    }

    let result = @reference();

    release(arr.buf);
    release(out.buf);

    result
}
